plugins {
    alias libs.plugins.android.application
    alias libs.plugins.kotlin.android
    alias libs.plugins.kotlin.kapt
    alias libs.plugins.dagger.android

    // Unable to use libs.safe.args.. dunno why...
    id "androidx.navigation.safeargs"
}

android {
    namespace 'one.njk.celestidesk'
    compileSdk 33

    // Load properties file containing our API key
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    def mailId = properties.getProperty('mail_id')
    def mailPwd = properties.getProperty('mail_pwd')
    def mailTo = properties.getProperty('mail_to')
    def baseUrl = properties.getProperty('base_url')

    defaultConfig {
        applicationId "one.njk.celestidesk"
        minSdk 30
        targetSdk 33
        versionCode 1
        versionName "1.0"

        buildConfigField "String" , "MAIL_ID" ,  mailId
        buildConfigField "String" , "MAIL_PWD" ,  mailPwd
        buildConfigField "String" , "MAIL_TO" ,  mailTo
        buildConfigField "String" , "BASE_URL" , baseUrl

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        viewBinding true
        dataBinding true
        buildConfig true
    }
}

dependencies {

    // androidx libs, layouts, material design
    implementation libs.bundles.androidx
    implementation libs.android.material

    // Testing
    testImplementation libs.junit
    androidTestImplementation libs.bundles.testing

    // Navigation graph support
    // see classpath in build.gradle (project)
    implementation libs.bundles.nav

    // Coroutines, coil, dagger hilt, retrofit
    implementation libs.coroutines
    implementation libs.bundles.retrofit
    implementation libs.dagger.android
    kapt libs.dagger.compiler

    // ROOM
    implementation libs.room.ktx
    kapt libs.room.compiler

    // Mail
    implementation libs.bundles.mail
}